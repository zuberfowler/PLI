######################### CLIST       ##################################
PROC 1 INDSN OUTDSN(COBFORM) PRIM(5) SEC(5) VOL() PARM() LIST
IF &LIST ^= THEN CONTROL LIST
CONTROL NOMSG
FREE ATTR(COBOUT)
FREE F(IN,OUT,SYSIN,SYSPRINT)
CONTROL MSG
ALLOC DA(*) F(SYSIN)
ALLOC DA(*) F(SYSPRINT)
IF &SUBSTR(1,&STR(&INDSN)) NE &STR(') +
    THEN DO
         SET &TDSN=&STR(&INDSN)
         SET &RETLAB=LABEL1
         GOTO GETMEM
         LABEL1:  SET &DINDSN=&STR(&TDSN).COBOL&TMEM
         END
    ELSE SET &DINDSN=&STR(&INDSN)
ALLOC DA(&DINDSN) F(IN) SHR
ATTR COBOUT REC(F,B) LREC(80) BLK(6160)
IF &VOL = +
    THEN ALLOC F(OUT) DA(&OUTDSN..COBOL) SPA(&PRIM,&SEC) +
             TRA CAT REL US(COBOUT)
    ELSE ALLOC F(OUT) DA(&OUTDSN..COBOL) SPA(&PRIM,&SEC) +
             TRA CAT REL US(COBOUT) VOL(&VOL
CALL 'SYS1.MONEYLIB(COBFORM)' '/&PARM'
GOTO FINISH
GETMEM:  IF &SUBSTR(1,&STR(&TDSN)) EQ &STR(') +
    THEN SET &TDSN=&SUBSTR(2:&LENGTH(&STR(&TDSN))-1,&STR(&TDSN))
SET &LENDSN=&LENGTH(&STR(&TDSN))
SET &TMEM=&STR()
SET &RPN=')'
SET &I=1
DO WHILE(&I LE &LENDSN AND &SUBSTR(&I:&I,&STR(&TDSN)X) NE &STR((
    SET &I=&I+1
    END
IF &I NE &LENDSN+1 +
THEN DO
     SET &J=&I+1
     DO WHILE(&SUBSTR(&J:&J,&STR(&TDSN)X) NE &SUBSTR(2:2,&RPN) +
       AND &J LE &LENDSN)
          SET &J=&J+1
          END
     SET &TMEM=&SUBSTR(&I:&J,&STR(&TDSN))
     SET &TDSN2=&SUBSTR(1:&I-1,&STR(&TDSN))
     IF &J NE &LENDSN +
         THEN SET &TDSN2=&STR(&TDSN2)&SUBSTR(&J+1:&LENDSN,&STR(&TDSN))
     SET &TDSN=&STR(&TDSN2)
     END
IF &LENGTH(&TDSN) LE 4 +
    THEN GOTO &RETLAB
IF &SUBSTR(&LENGTH(&TDSN)-4:&LENGTH(&TDSN),&STR(&TDSN)) +
   EQ &STR(COBOL) +
    THEN SET &TDSN=&SUBSTR(1:&LENGTH(&TDSN)-6,&STR(&TDSN))
GOTO &RETLAB
FINISH: END
######################### PROC        ##################################
//COBFORM  PROC OPT='NC,NR,O2,PC,UC'
//CF       EXEC  PGM=COBFORM,PARM='/&OPT'
//* VALID PARAMETERS
//* AI -ALTERNATE INDENTATION METHOD
//* CS -COMPRESS SOURCE
//* CO -COUNT INPUT AND OUTPUT LINES
//* DO -PROCESS DATA DIVISION ONLY
//* GD -SAME AS:  AI,OT,PC,UC
//* KC -KEEP COMMAS IN SOURCE
//* KS -KEEP SPACING COMMANDS IN SOURCE
//* LO -LIST ONLY
//* LI -LIST OUTPUT
//* NC -NO COMMENTS, COMMENTS ARE NOT FORMATTED
//* NR -NO REMARKS, REMARKS ARE NOT FORMATTED
//* NS -NO SUBSTITUITIONS, LONG FORM KEYWORDS RETAINED
//* OT -OMIT THENS
//* O2 -COPY OF OUTPUT DATASET WRITTEN TO OUT2, MAY BE SYSOUT
//* PC -PIC COLUMN, PLACE PIC'S STARTING IN COLUMN 37
//* PO -PROCESS PROCEDURE DIVISION ONLY
//* UC -USING COLUMN, PLACE PARAMETERS AFTER USING IN VERTICAL COLUMN
//* WM -WIDE MARGINS, USE COLUMNS 7-80 AS SOURCE FIELD
//IN       DD  DSN=&&PANOUT,DISP=(OLD,DELETE)
//OUT      DD  DISP=SHR
//OUT2     DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
