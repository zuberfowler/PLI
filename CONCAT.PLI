*PROCESS NM;
 CONCAT:  PROCEDURE(CONPARM) OPTIONS(MAIN, REENTRANT) REORDER;
 DECLARE  PGMID  CHAR(35) VARYING STATIC EXTERNAL
     INIT('CONCAT  V1.1  BY CLYDE THOMAS ZUBER');
 DECLARE  PLIXOPT  CHAR(7) VARYING STATIC EXTERNAL INIT('ISA(4K)');

 /*********************************************************************/
 /*                                                                   */
 /* THIS PROGRAM DOES CONCATENATION OF DDNAMES TO THE FIRST DDNAME    */
 /* IN THE LIST.  THE LIST IS PASSED AS A SEQUENCE IN THE PARAMETER.  */
 /*                                                                   */
 /*********************************************************************/

 DECLARE  CONPARM   CHAR(100) VARYING;
 DECLARE  PARAMETER CHAR(100) VARYING;

 DECLARE  SVC      ENTRY OPTIONS(ASM, INTER, RETCODE);
 DECLARE  ENDLIST  ENTRY OPTIONS(ASM, INTER);
 DECLARE  (ADDR, INDEX, LENGTH, PLIRETV, SUBSTR)  BUILTIN;
 DECLARE  SYSPRINT  FILE OUTPUT STREAM PRINT;

 DECLARE  CON_TEXTPTR      PTR INIT(ADDR(TF1));
 DECLARE  PTR_CON_REQUEST  PTR INIT(ADDR(CON_REQUEST_BLK));

 DECLARE
     1  CON_REQUEST_BLK,
        2  BLK_LENGTH      BIT(8)  INIT('00010100'B),
        2  VERB            BIT(8)  INIT('00000011'B),
        2  FLAGS_ERR_INFO  BIT(48) INIT('0'B),
        2  PTR_TEXTPTRS    PTR     INIT(ADDR(CON_TEXTPTR)),
        2  RESERVED_FLAGS  BIT(64) INIT('0'B);

 DECLARE  /* DDNAME */
     1  TF1,
        2  KEY  FIXED BINARY INIT(1),
        2  NUM  FIXED BINARY INIT(0),
        2  PARMS(16),
           3  HEX_LEN  BIT(16) INIT((16)(1)'0000000000001000'B),
           3  PARM     CHAR(8);

 DECLARE  R1   PTR INIT(ADDR(PTR_CON_REQUEST));
 DECLARE  R0   FIXED BINARY(31) INIT(0);
 DECLARE  #99  FIXED BINARY STATIC INIT(99);
 DECLARE  L    FIXED BINARY;
1CALL ENDLIST(PTR_CON_REQUEST);
 CALL ENDLIST(CON_TEXTPTR);
 PARAMETER = CONPARM;
 IF PARAMETER = ''
     THEN DO;
          PUT PAGE EDIT('DDNAMES NOT PASSED IN PARM') (A);
          SIGNAL ERROR;
          END;

 PARAMETER = PARAMETER ]] ',';
 L = INDEX(PARAMETER, ',');
 DO WHILE(L > 0);
     NUM = NUM + 1;
     PARM(NUM) = SUBSTR(PARAMETER, 1, L-1);
     IF LENGTH(PARAMETER) = L
         THEN PARAMETER = '';
         ELSE PARAMETER = SUBSTR(PARAMETER, L+1);
     L = INDEX(PARAMETER, ',');
     END;

 CALL SVC(#99, R0, R1);

 IF PLIRETV ^= 0
     THEN DO;
          PUT SKIP(3) EDIT('CONCATENATION ERROR, RETURN CODE =',
              PLIRETV) (A, F(10));
          SIGNAL ERROR;
          END;

 END CONCAT;
